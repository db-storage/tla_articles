# WriteThroughCache

本文介绍的WriteThroughCache来自于TLA+ book，对原文中的例子进行分析。以加深对Spec和Model Check的理解。这个模型本身是个成熟的模型。

## 1. 什么是 Write Through Cache

- 写穿模型，简单来说，在写Cache的同时，也要写主存
- 与Write Back相对应，Write Back是先写到Cache，在合适的时候异步写回主存
- 在下面要解释的TLA Spec 中，在一次状态变更中，完成写Cache的同时，产生更新主存的请求(只是请求进入队列。而真正的写主存，则由单独的状态变更完成)。



## 2. 模型

### 图

### 组成部分



- 多进程
- 每个进程有自己的Cache
- 每个进程最多只有一个待执行的请求(包括读/写)，这个通过ctl[p]的状态来控制
- 主存的读写操作按照FIFO方式进行



## 3. 支持的接口

### 1. Read



### 2. Write

### 3. Evict

- 由于Evict哪个cache项以及什么时候Evict不影响正确性，只影响效率，所以随机选择一个Cache元素去Evict 

  

## 4. Spec 解析

#### 与图中元素对应

| 图中元素 | spec中变量 | 说明               |
| -------- | ---------- | ------------------ |
| 主存     | wmem       | wmem是个function   |
| ctl      | ctl        | ctl是个function    |
| buf      | buf        | 是一个function     |
| cache    | cache      | cache是个 function |
| Queue    | memQ       | memQ是个Sequence   |



#### 常量和变量定义

```tla
EXTENDS Naturals, Sequences 
VARIABLES wmem, ctl, buf, cache, memQ 

CONSTANTS QLen,
          Proc,  
           Adr,  
           Val
(* 认为QLen必须是大于0的自然数                                          *)
ASSUME (QLen \in Nat) /\ (QLen > 0)
```



其中Qlen, Proc, Adr, Val是常量，需要用户在运行TLC Model Checker前定义。

| 常量 | 意义                                                        |
| ---- | ----------------------------------------------------------- |
| QLen | 限定memQ的最大长度，用于减少可能的状态数量                  |
| Proc | Process的集合，也是ctl, buf等Function的Domain               |
| Addr | 主存的地址空间，运行model checker时，只需要少量几个空间即可 |
| Val  | 每个内存地址可能的有效值                                    |



#### 请求的集合(取值范围)

```tla
(* 请求的取值范围，"Rd"和"Wr"两种Record的集合。                                    *)
(* Rd"类型的，只需要指定adr， 而 "Wr"则还需要指定val                                *)
(* “Rd"类型和"Wr"类型的请求，由于adr可以取Val的不同值，实际上是个集合。"Wr"类型也类似    *)
(* \cup是并集的意思                                                             *)
MReq == [op : {"Rd"}, adr: Adr] 
          \cup [op : {"Wr"}, adr: Adr, val : Val]

(* NoVal是一个不存在的值，类似于NULL/nil的含义                                     *)
NoVal == CHOOSE v : v \notin Val
```



#### 初始状态

```tla
(* wmem中的每个地址对应的值可以是任意合法值， ctl中每个元素必须是"rdy"，才可接受新请求  *)
(* 其他的取值为NoVal，表示无效值                                                *)
Init == /\ wmem \in [Adr->Val]
        /\ ctl = [p \in Proc |-> "rdy"] 
        /\ buf = [p \in Proc |-> NoVal] 
        /\ cache = [p \in Proc |-> [a \in Adr |-> NoVal] ]
        /\ memQ = << >>  
```



#### 不变式

```tla
(* TypeInv 表示变量的取值，是在合法范围，即：类型是正确的。与前面表格中描述内容一致  *)
(* 比如，wmem这个function，它的Domain是Adr                                  *)
(* memQ 是个Sequence，Sequence内部的每个元素，是个二元祖(tuple)               *)
(* \X表示笛卡尔积，也就是说，元祖的第一元表示取值范围是Proc, 第二元取值范围是MReq   *)
TypeInvariant == 
  /\ wmem  \in [Adr -> Val]
  /\ ctl   \in [Proc -> {"rdy", "busy", "waiting", "done"}] 
  /\ buf   \in [Proc -> MReq \cup Val \cup {NoVal}]
  /\ cache \in [Proc -> [Adr -> Val \cup {NoVal}]] 
  /\ memQ  \in Seq(Proc \X MReq) 

(* Coherence 则关注内容是不是对的，以及变量之间的关系                 *)
Coherence == \A p, q \in Proc, a \in Adr : 
                (NoVal \notin {cache[p][a], cache[q][a]})
                      => (cache[p][a]=cache[q][a]) 
```





#### 接收请求和响应的操作

```tla
Req(p) == /\ ctl[p] = "rdy" 
          /\ \E req \in  MReq :
                /\ buf' = [buf EXCEPT ![p] = req]
                /\ ctl' = [ctl EXCEPT ![p] = "busy"]
          /\ UNCHANGED <<cache, memQ, wmem>>  


Rsp(p) == /\ ctl[p] = "done"
          /\ ctl' = [ctl EXCEPT ![p]= "rdy"]
          /\ UNCHANGED <<cache, memQ, wmem, buf>> 
 
```



#### 内部处理操作

```tla
(* 有读请求，且缓存未命中，将读请求放到memQ中                            *)
(* 前三行都是前提条件，如果都满足，才执行后续的状态变更                    *)
(* ctl的新旧状态差异，只在于ctl[p]，ctl的其他成员不变                    *)
RdMiss(p) ==  /\ (ctl[p] = "busy") /\ (buf[p].op = "Rd") 
              /\ cache[p][buf[p].adr] = NoVal 
              /\ Len(memQ) < QLen
              /\ memQ' = Append(memQ, <<p, buf[p]>>)
              /\ ctl' = [ctl EXCEPT ![p] = "waiting"]
              /\ UNCHANGED <<wmem, buf, cache>>
             
(*从缓存读*)
DoRd(p) == 
  /\ ctl[p] \in {"busy","waiting"} 
  /\ buf[p].op = "Rd" 
  /\ cache[p][buf[p].adr] # NoVal
  /\ buf' = [buf EXCEPT ![p] = cache[p][buf[p].adr]]
  /\ ctl' = [ctl EXCEPT ![p] = "done"]
  /\ UNCHANGED <<wmem, cache, memQ>> 

(*写主存*)
DoWr(p) == 
  LET r == buf[p]
  IN  /\ (ctl[p] = "busy") /\ (r.op = "Wr") 
      /\ Len(memQ) < QLen
      /\ cache' = [q \in Proc |-> 
                    IF (p=q)  \/  (cache[q][r.adr]#NoVal)
                      THEN [cache[q] EXCEPT ![r.adr] = r.val]
                      ELSE cache[q] ]
      /\ memQ' = Append(memQ, <<p, r>>)
      /\ buf' = [buf EXCEPT ![p] = NoVal]
      /\ ctl' = [ctl EXCEPT ![p] = "done"]
      /\ UNCHANGED <<wmem>> 
```



#### vmem

vmem类似于数据库里面的view，它不是一个变量的值，而是要动态产生的值。

```tla
(* wmem 是个 Adr -> Val 的Function                                           *)
(* f[0] =  wmem, 所以  f[0] 也是一个Adr -> Val 的 Function                    *)
(* f 是一个 [0 .. Len(memQ)] -> ([Adr] -> Val) 的 Function                   *)
(* vmem = f[Len(memQ)], 加设memQ有3个元素，那么vmem = f[3]                     *)

(* For i > 0,                                                               *)
(*     IF memQ[i] is a "Rd", skip it, so f[i] = f[i-1]                      *)
(*     ELSE f[i] = f[i-1] EXECPT f[i][memQ[i][2].adr] =memQ[i][2].val       *)
(* vmem的含义: wmem + memQ中的写请求带来的修改                                  *)
vmem  ==  
  LET f[i \in 0 .. Len(memQ)] == 
        IF i=0 THEN wmem
               ELSE IF memQ[i][2].op = "Rd"
                      THEN f[i-1]
                      ELSE [f[i-1] EXCEPT ![memQ[i][2].adr] =
                                              memQ[i][2].val]
  IN f[Len(memQ)] 
```



#### MemQ操作

```tla
MemQWr == LET r == Head(memQ)[2] 
          IN  /\ (memQ # << >>)  /\   (r.op = "Wr") 
              /\ wmem' = [wmem EXCEPT ![r.adr] = r.val] 
              /\ memQ' = Tail(memQ) 
              /\ UNCHANGED <<buf, ctl, cache>> 

MemQRd == 
  LET p == Head(memQ)[1] 
      r == Head(memQ)[2] 
  IN  /\ (memQ # << >> ) /\ (r.op = "Rd")
      /\ memQ' = Tail(memQ)
      /\ cache' = [cache EXCEPT ![p][r.adr] = vmem[r.adr]]
      /\ UNCHANGED <<wmem, buf, ctl>>  
```



```tla
Evict(p,a) == /\ (ctl[p] = "waiting") => (buf[p].adr # a) 
              /\ cache' = [cache EXCEPT ![p][a] = NoVal]
              /\ UNCHANGED <<wmem, buf, ctl, memQ>> 
```

#### Nex 和 Spec

```tla
Next == \/ \E p\in Proc : \/ Req(p) \/ Rsp(p) 
                          \/ RdMiss(p) \/ DoRd(p) \/ DoWr(p) 
                          \/ \E a \in Adr : Evict(p, a)
        \/ MemQWr \/ MemQRd    

Spec == 
  Init /\ [][Next]_<<wmem, buf, ctl, cache, memQ>>
```



### 为什么需要MemQRd 和 MemQWr？






## 尝试做些改动

### 改变下Coherence



```tla

Coherence == \A p, q \in Proc, a \in Adr : 
                (NoVal \notin {cache[p][a], cache[q][a]})
                      => (cache[p][a]=cache[q][a]) 

```

修改为：

```tla

Coherence == \A p \in Proc, a \in Adr : 
                (NoVal \neq cache[p][a]) =>
                      => (cache[p][a]= vmem[a]) 
```





### 不读vmem，直接读取wmem

### 每个Process可以有多个并发请求



## 如何对应到代码实现

### - DoWrite()操作的原子性保证？锁总线



使用Graphviz来可视化状态变迁

